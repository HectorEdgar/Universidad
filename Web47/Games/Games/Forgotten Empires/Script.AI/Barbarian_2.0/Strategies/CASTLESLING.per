(defrule
	(or	(and	(up-compare-goal gl-strategy != castle-sling)
			(game-time > 1200))
		(or	(goal gl-nothing-style-map YES)
			(or	(up-compare-const gv-michi-style == 1)
				(and	(game-time > 20)
					(up-compare-goal POSITION != POCKET)))))
=>
	(up-jump-rule 28)
)

(defrule
	(game-time > 10)
=>
	(set-goal gl-strategy-aggressiveness-level LOW)
	(set-goal gl-strategy-type FC2)
	(set-goal gl-strategy regicide-strategy)
	(disable-self)
)

(defrule
	(current-age == dark-age)
	(game-time > 40)
	(game-time < 50)
	(goal SLING 0)
	(taunt-detected every-ally 254)
	(strategic-number sn-minimum-water-body-size-for-dock == 998)
;	(goal POSITION POCKET)
	(up-allied-goal every-ally SLING == 0)
	(nor	(taunt-detected any-ally 103)
		(taunt-detected any-ally 104))
	(nor	(taunt-detected my-player-number 103)
		(taunt-detected my-player-number 104))
=>
	(acknowledge-taunt every-ally 1)
	(chat-to-allies "176Does anyone want a castle age sling?")
	(disable-self)
)

(defrule
	(goal gl-current-age gv-dark)
	(game-time > 20)
	(game-time < 50)
	(goal SLING 0)
	(taunt-detected every-ally 254)
	(strategic-number sn-minimum-water-body-size-for-dock == 998)
	(goal POSITION POCKET)
	(taunt-detected my-player-number 176)
	(taunt-detected any-ally 1)
=>
	(acknowledge-taunt every-ally 1)
	(set-goal gl-strategy-aggressiveness-level VERY-LOW)
	(set-goal gl-strategy-type FC)
	(set-goal gl-strategy castle-sling)
	(disable-self)
)

(defrule
	(goal gl-strategy castle-sling)
	(strategic-number sn-allow-resource-spending > 0)
	(not	(research-completed ri-banking))
=>
	(set-strategic-number sn-allow-resource-spending 0)
)

(defrule
	(goal gl-strategy castle-sling)
	(up-compare-goal threat-time < 10000)
	(strategic-number sn-allow-resource-spending == 0)
	(research-completed ri-banking)
=>
	(set-strategic-number sn-allow-resource-spending 1)
)

(defrule
	(goal gl-strategy castle-sling)
	(building-type-count-total market < 1)
	(can-build market)
=>
	(set-strategic-number sn-maximum-town-size 10)
	(build market)
)

(defrule
	(goal gl-strategy castle-sling)
	(building-type-count-total market > 0)
	(building-type-count-total blacksmith < 1)
	(can-build blacksmith)
=>
	(set-strategic-number sn-maximum-town-size 10)
	(build blacksmith)
)

(defrule
	(goal gl-strategy castle-sling)
	(up-compare-goal gl-current-age >= gv-castle)
	(unit-type-count-total villager > 45)
	(can-research ri-coinage)
=>
	(research ri-coinage)
)

(defrule
	(goal gl-strategy castle-sling)
	(up-compare-goal threat-time < 7000)
	(or	(unit-type-count-total my-unique-unit-line < 4)
		(game-time > 1600))
	(or	(unit-type-count-total my-unique-unit-line < 9)
		(game-time > 2100))
	(unit-type-count-total my-unique-unit-line < 20)
	(or	(can-train my-unique-unit-line)
		(can-train my-UU))
=>
	(train my-unique-unit-line)
	(train my-unique-unit)
)

(defrule
	(goal gl-strategy castle-sling)
	(game-time > 2500)
	(building-type-count-total town-center < 4)
	(can-build-with-escrow town-center)
=>
	(set-strategic-number sn-town-center-placement 0)
	(release-escrow wood)
	(build town-center)
	(chat-to-player my-player-number "Town center")
)

(defrule
	(goal gl-strategy castle-sling)
	(or	(building-type-count-total town-center == 1)
		(building-type-count-total town-center == 3))
	(can-build-with-escrow town-center)
=>
	(up-modify-sn sn-camp-max-distance g:= gl-my-town-size)
	(set-strategic-number sn-town-center-placement mining-camp)
	(release-escrow wood)
	(build town-center)
	(chat-to-player my-player-number "Town center")
)

(defrule
	(goal gl-strategy castle-sling)
	(building-type-count-total town-center == 2)
	(can-build-with-escrow town-center)
=>
	(up-modify-sn sn-camp-max-distance g:= gl-my-town-size)
	(set-strategic-number sn-town-center-placement lumber-camp)
	(release-escrow wood)
	(build town-center)
	(chat-to-player my-player-number "Town center")
)

(defrule
	(goal gl-strategy castle-sling)
	(current-age >= castle-age)
	(research-completed ri-horse-collar)
	(or	(and	(building-type-count-total town-center > 2)
			(research-completed ri-heavy-plow))
		(idle-farm-count < 1))
	(idle-farm-count < 3)
	(can-build farm)
=>
	(build farm)
)

(defrule
	(goal gl-strategy castle-sling)
	(unit-type-count-total villager > 45)
	(can-research ri-banking)
=>
	(research ri-banking)
	(enable-timer 4 34)
	(enable-timer 6 0)
	(set-strategic-number sn-target-evaluation-ally-proximity 9999)
	(chat-to-allies "I'm getting banking!")
)

(defrule
	(goal gl-strategy castle-sling)
	(building-type-count-total town-center > 2)
	(unit-type-count-total villager > 40)
	(research-completed ri-banking)
=>
	(set-goal SLING 5)
	(set-strategic-number sn-enable-training-queue 1)
	(enable-timer 4 0)
	(enable-timer 6 0)
	(disable-self)
)

(defrule
	(up-compare-goal gl-current-age >= gv-castle)
	(goal gl-strategy castle-sling)
	(game-time > 1200)
	(or	(taunt-detected any-ally 97)
		(taunt-detected any-human-ally 33))
=>
	(acknowledge-taunt every-ally 1)
	(acknowledge-taunt every-ally 33)
	(acknowledge-taunt every-ally 97)
	(acknowledge-taunt every-ally 123)
	(enable-timer 28 10)
	(chat-to-allies "176I stop slinging you now, does anyone else want res?")
	(chat-to-player every-ally "96")
	(chat-to-player my-player-number "172")
)

(defrule
	(up-compare-goal gl-current-age >= gv-castle)
	(goal gl-strategy castle-sling)
	(taunt-detected my-player-number 172)
	(game-time > 1200)
	(or	(taunt-detected any-ally 175)
		(taunt-detected any-human-ally 1))
=>
	(acknowledge-taunt every-ally 1)
	(acknowledge-taunt every-ally 175)
	(acknowledge-taunt every-ally 123)
	(disable-timer 28)
	(chat-to-player this-any-ally "173")
)

(defrule
	(goal gl-strategy castle-sling)
	(or	(timer-triggered 28)
		(or	(game-time > 3900)
			(up-compare-goal gl-current-age >= gv-castle-up)))
=>
	(chat-to-player every-ally "96I stop slinging now")
	(chat-to-player my-player-number "I stop slinging now")
	(set-goal gl-strategy default)
	(set-goal SLING 0)
	(disable-self)
)

(defrule
	(goal gl-strategy castle-sling)
	(unit-type-count-total villager > 140)
=>
	(set-strategic-number sn-enable-training-queue 0)
	(disable-self)
)

(defrule
	(goal gl-strategy castle-sling)
	(goal gl-current-age gv-castle)
	(not	(can-research-with-escrow ri-hand-cart))
	(unit-type-count-total trade-cart < 15)
	(population < 185)
	(unit-type-count-total villager < 150)
	(can-train villager)
=>
	(train villager)
)

(defrule
	(goal gl-strategy castle-sling)
	(building-type-count-total town-center > 2)
	(goal SLING 5)
	(unit-type-count-total villager > 90)
	(or	(can-research ri-gold-shaft-mining)
		(can-research ri-stone-shaft-mining))
=>
	(research ri-gold-shaft-mining)
	(research ri-stone-shaft-mining)
)

(defrule
	(goal gl-strategy castle-sling)
	(up-pending-objects c: mining-camp < 1)
	(building-type-count-total town-center > 3)
	(game-time > 2100)
	(building-type-count-total mining-camp < 5)
	(can-build mining-camp)
=>
	(set-strategic-number sn-allow-adjacent-dropsites 0)
	(build mining-camp)
)

; ================ ECO

(defrule
	(goal gl-strategy castle-sling)
	(goal gl-current-age gv-dark-up)
	(or	(strategic-number sn-food-gatherer-percentage != 39)
		(strategic-number sn-wood-gatherer-percentage != 43))
=>
	(set-strategic-number sn-food-gatherer-percentage 39)
	(set-strategic-number sn-wood-gatherer-percentage 43)
	(set-strategic-number sn-gold-gatherer-percentage 12)
	(set-strategic-number sn-stone-gatherer-percentage 7)
)

(defrule
	(goal gl-strategy castle-sling)
	(up-compare-goal gl-current-age >= gv-feudal-up)
	(building-type-count-total castle == 0)
	(stone-amount < castle-stone)
	(or	(strategic-number sn-food-gatherer-percentage != 30)
		(strategic-number sn-gold-gatherer-percentage != 36))
=>
	(set-strategic-number sn-food-gatherer-percentage 30)
	(set-strategic-number sn-wood-gatherer-percentage 36)
	(set-strategic-number sn-gold-gatherer-percentage 5)
	(set-strategic-number sn-stone-gatherer-percentage 29)
)

(defrule
	(goal gl-strategy castle-sling)
	(up-compare-goal gl-current-age >= gv-feudal-up)
	(or	(and	(stone-amount >= 650)
			(goal gl-current-age gv-feudal-up))
		(building-type-count-total castle > 0))
=>
	(set-strategic-number sn-food-gatherer-percentage 36)
	(set-strategic-number sn-wood-gatherer-percentage 50)
	(set-strategic-number sn-gold-gatherer-percentage 8)
	(set-strategic-number sn-stone-gatherer-percentage 6)
	(disable-self)
)

(defrule
	(goal gl-strategy castle-sling)
	(up-compare-goal gl-current-age >= gv-feudal-up)
	(current-age >= castle-age)
	(building-type-count-total town-center > 2)
	(or	(strategic-number sn-food-gatherer-percentage != 46)
		(strategic-number sn-gold-gatherer-percentage != 37))
=>
	(set-strategic-number sn-food-gatherer-percentage 47)
	(set-strategic-number sn-wood-gatherer-percentage 36)
	(set-strategic-number sn-gold-gatherer-percentage 14)
	(set-strategic-number sn-stone-gatherer-percentage 3)
)

(defrule
	(goal gl-strategy castle-sling)
	(up-compare-goal gl-current-age >= gv-feudal-up)
	(current-age >= castle-age)
	(building-type-count-total town-center > 2)
	(unit-type-count-total villager > 45)
	(research-completed ri-banking)
	(or	(strategic-number sn-food-gatherer-percentage != 40)
		(strategic-number sn-gold-gatherer-percentage != 35))
=>
	(set-strategic-number sn-food-gatherer-percentage 40)
	(set-strategic-number sn-wood-gatherer-percentage 35)
	(set-strategic-number sn-gold-gatherer-percentage 19)
	(set-strategic-number sn-stone-gatherer-percentage 6)
)

(defrule
	(goal gl-strategy castle-sling)
	(up-compare-goal gl-current-age >= gv-feudal-up)
	(current-age >= castle-age)
	(building-type-count-total town-center > 2)
	(unit-type-count-total villager > 100)
	(research-completed ri-banking)
	(or	(strategic-number sn-food-gatherer-percentage != 38)
		(strategic-number sn-gold-gatherer-percentage != 33))
=>
	(set-strategic-number sn-food-gatherer-percentage 40)
	(set-strategic-number sn-wood-gatherer-percentage 35)
	(set-strategic-number sn-gold-gatherer-percentage 23)
	(set-strategic-number sn-stone-gatherer-percentage 6)
)

#load-if-not-defined UP-4-PLAYER-GAME
(defrule
	(goal gl-strategy castle-sling)
	(up-compare-goal threat-time > 25000)
	(building-type-count-total town-center > 3)
	(game-time > 2000)
	(population > 130)
	(unit-type-count trade-cart > 0)
	(research-completed ri-cartography)
	(building-type-count-total market < 3)
	(players-building-type-count any-ally market > 0)
	(building-type-count town-center > 0)
	(can-build market)
=>
	(up-set-placement-data my-player-number -1 c: -100)
	(up-build place-control 0 c: market)
	(set-strategic-number sn-object-repair-level 20611)
	(set-strategic-number sn-placement-fail-delta -1)
)
#else
(defrule
	(goal gl-strategy castle-sling)
	(up-compare-goal threat-time > 25000)
	(building-type-count-total town-center > 3)
	(game-time > 2000)
	(population > 130)
	(unit-type-count trade-cart > 0)
	(research-completed ri-cartography)
	(building-type-count-total market < 3)
	(players-building-type-count any-ally market > 0)
	(building-type-count town-center > 0)
	(can-build market)
=>
	(set-strategic-number sn-placement-to-center 1)
	(up-set-placement-data any-ally -1 c: -130)
	(up-build place-control 0 c: market)
	(set-strategic-number sn-object-repair-level 20611)
	(set-strategic-number sn-placement-fail-delta -1)
)
#end-if

#load-if-defined HUN-CIV
(defrule
	(goal gl-strategy castle-sling)
	(unit-type-count-total tarkan-line > 12)
	(strategic-number sn-allow-resource-spending > 1)
	(building-type-count farm > 17)
	(unit-type-count-total villager > 50)
	(or	(research-available ri-scale-barding)
		(research-available ri-chain-barding))
=>
	(set-escrow-percentage food 70)
	(set-escrow-percentage gold 60)
)

(defrule
	(goal gl-strategy castle-sling)
	(unit-type-count-total tarkan-line > 3)
	(strategic-number sn-allow-resource-spending > 0)
	(or	(can-research-with-escrow ri-scale-barding)
		(and	(can-research-with-escrow ri-chain-barding)
			(strategic-number sn-allow-resource-spending > 1)))
=>
	(release-escrow food)
	(release-escrow gold)
	(set-escrow-percentage food 0)
	(set-escrow-percentage gold 0)
	(research ri-scale-barding)
	(research ri-chain-barding)
)

(defrule
	(goal gl-strategy castle-sling)
	(strategic-number sn-allow-resource-spending > 2)
	(unit-type-count-total tarkan-line > 5)
	(or	(can-research ri-forging)
		(and	(or	(strategic-number sn-allow-resource-spending > 3)
				(and	(up-compare-goal gl-current-age <= gv-castle)
					(town-under-attack)))
			(can-research ri-iron-casting)))
=>
	(research ri-forging)
	(research ri-iron-casting)
)

(defrule
	(goal gl-strategy castle-sling)
	(strategic-number sn-allow-resource-spending > 2)
	(or	(strategic-number sn-allow-resource-spending > 3)
		(and	(goal MILITARY 1)
			(and	(up-compare-goal gl-current-age <= gv-castle)
				(research-completed ri-forging))))
	(research-completed ri-chain-barding)
	(unit-type-count-total tarkan-line > 5)
	(can-research ri-husbandry)
=>
	(research ri-husbandry)
)
#end-if
