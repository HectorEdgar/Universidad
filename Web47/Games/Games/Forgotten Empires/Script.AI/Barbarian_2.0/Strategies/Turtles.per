(defrule
	(up-compare-goal gl-strategy != turtle-rush)
	(game-time > 1200)
=>
	(up-jump-rule 14)
)

(defrule
	(game-time > 50)
	(goal gl-current-age gv-dark)
	(goal SLING 0)
	(cc-players-unit-type-count 0 gold-mine > average-fish-amount); Don't do turtles if map has low gold
	(strategic-number sn-minimum-water-body-size-for-dock == 2001)
=>
	(set-goal DOCK 12)
	(set-goal gl-strategy turtle-rush)
	(set-goal gl-strategy-aggressiveness-level LOW)
	(set-goal gl-strategy-type FC2)
	(set-strategic-number sn-dock-avoidance-factor -10000)
	(set-strategic-number sn-dock-proximity-factor 200)
	(set-strategic-number sn-dock-placement-mode -1)
	(set-strategic-number sn-allow-new-build-order 2)
	(disable-self)
)

(defrule
	(game-time > 50)
	(goal gl-current-age gv-dark)
	(goal SLING 0)
	(or	(map-type mediterranean)
		(or	(map-type baltic)
			(map-type crater-lake)))
=>
	(set-goal DOCK 12)
	(set-goal gl-strategy turtle-rush)
	(set-goal gl-strategy-aggressiveness-level LOW)
	(set-goal gl-strategy-type FC2)
	(set-strategic-number sn-dock-proximity-factor 200)
	(set-strategic-number sn-dock-placement-mode -1)
	(set-strategic-number sn-allow-new-build-order 2)
	(disable-self)
)

; ============ BUILDINGS

(defrule
	(goal gl-strategy turtle-rush)
	(building-type-count-total market < 1)
	(can-build market)
=>
	(set-strategic-number sn-maximum-town-size 10)
	(build market)
)

(defrule
	(goal gl-strategy turtle-rush)
	(building-type-count-total market > 0)
	(building-type-count-total blacksmith < 1)
	(can-build blacksmith)
=>
	(set-strategic-number sn-maximum-town-size 10)
	(build blacksmith)
)

(defrule
	(goal gl-strategy turtle-rush)
	(current-age == dark-age)
	(building-type-count dock > 0)
	(unit-type-count-total villager > 25)
	(dropsite-min-distance deer-hunting > 7)
	(dropsite-min-distance deer-hunting < 27)
	(strategic-number sn-enable-boar-hunting < 2)
	(building-type-count-total mill < 2)
	(can-build mill)
=>
	(set-strategic-number sn-preferred-mill-placement 1)
	(build mill)
)

(defrule
	(goal gl-strategy turtle-rush)
	(building-type-count-total castle < 1)
	(can-build castle)
=>
	(up-set-placement-data my-player-number dock c: 1)
	(set-strategic-number sn-placement-zone-size 5)
	(up-build place-control 0 c: castle)
	(set-strategic-number sn-stone-gatherer-percentage 6)
	(chat-to-player my-player-number "Castle!")
	(set-goal gl-castle-placed YES)
	(set-goal gl-castle-control YES)
	(set-goal castle-dropped-timer 20)
)

(defrule
	(goal gl-strategy turtle-rush)
	(nand	(food-amount > 800)
		(and	(unit-type-count-total turtle-ship-line > 15)
			(or	(research-available imperial-age)
				(research-available ri-elite-turtle-ship))))
	(unit-type-count-total turtle-ship-line < 100)
	(can-train turtle-ship-line)
=>
	(train turtle-ship-line)
)


(defrule
	(goal gl-strategy turtle-rush)
	(not	(research-completed ri-elite-turtle-ship))
	(players-current-age focus-player > castle-age)
	(strategic-number sn-threat-level != -2)
=>
	(set-strategic-number sn-threat-level -2)
)

(defrule
	(goal gl-strategy turtle-rush)
	(unit-type-count turtle-ship-line > 0)
	(not	(research-completed ri-elite-turtle-ship))
	(players-current-age focus-player >= castle-age)
	(strategic-number sn-threat-level != -4)
=>
	(set-strategic-number sn-threat-level -4)
)

(defrule
	(goal gl-strategy turtle-rush)
	(unit-type-count turtle-ship-line > 0)
	(or	(players-current-age focus-player < castle-age)
		(research-completed ri-elite-turtle-ship))
	(strategic-number sn-threat-level != -5)
=>
	(set-strategic-number sn-threat-level -5)
)

; =============== ECO

(defrule
	(goal gl-strategy turtle-rush)
	(building-type-count-total castle < 1)
	(goal gl-current-age gv-dark-up)
=>
	(set-strategic-number sn-stone-gatherer-percentage 4)
	(disable-self)
)

(defrule
	(goal gl-strategy turtle-rush)
	(up-compare-goal gl-current-age >= gv-dark-up)
	(current-age == dark-age)
	(or	(strategic-number sn-food-gatherer-percentage != 40)
		(or	(strategic-number sn-wood-gatherer-percentage != 47)
			(or	(strategic-number sn-gold-gatherer-percentage != 13)
				(strategic-number sn-skip-first-mining-camp != 0))))
=>
	(set-strategic-number sn-food-gatherer-percentage 40)
	(set-strategic-number sn-wood-gatherer-percentage 47)
	(set-strategic-number sn-gold-gatherer-percentage 13)
	(set-strategic-number sn-skip-first-mining-camp 0)
)

(defrule
	(goal gl-strategy turtle-rush)
	(up-compare-goal gl-current-age >= gv-feudal-up)
	(building-type-count-total castle == 0)
	(stone-amount < castle-stone)
	(or	(strategic-number sn-food-gatherer-percentage != 10)
		(strategic-number sn-gold-gatherer-percentage != 9))
=>
	(set-strategic-number sn-food-gatherer-percentage 10)
	(set-strategic-number sn-wood-gatherer-percentage 47)
	(set-strategic-number sn-gold-gatherer-percentage 9)
	(set-strategic-number sn-stone-gatherer-percentage 33)
)

(defrule
	(goal gl-strategy turtle-rush)
	(up-compare-goal gl-current-age >= gv-feudal-up)
	(or	(and	(stone-amount >= 650)
			(goal gl-current-age gv-feudal-up))
		(building-type-count-total castle > 0))
=>
	(set-strategic-number sn-food-gatherer-percentage 16)
	(set-strategic-number sn-wood-gatherer-percentage 53)
	(set-strategic-number sn-gold-gatherer-percentage 28)
	(set-strategic-number sn-stone-gatherer-percentage 3)
	(disable-self)
)

(defrule
	(goal gl-strategy turtle-rush)
	(building-type-count town-center > 2)
	(up-compare-goal gl-current-age >= gv-feudal-up)
	(or	(and	(stone-amount >= 650)
			(goal gl-current-age gv-feudal-up))
		(building-type-count-total castle > 0))
=>
	(set-strategic-number sn-food-gatherer-percentage 23)
	(set-strategic-number sn-wood-gatherer-percentage 45)
	(set-strategic-number sn-gold-gatherer-percentage 29)
	(set-strategic-number sn-stone-gatherer-percentage 3)
	(disable-self)
)

