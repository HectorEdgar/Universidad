#load-if-not-defined ARENA-MAP
#load-if-not-defined BLACK-FOREST-MAP
(defrule
	(game-time > 300)
	(game-time < 310)
	(or	(strategic-number sn-minimum-water-body-size-for-dock == 998)
		(or	(map-type yucatan)
			(or	(map-type baltic)
				(map-type mediterranean))))
=>
	(set-goal gl-allow-wonders YES)
	(disable-self)
)

(defrule
	(or	(up-compare-goal gl-allow-wonders != YES)
		(or	(enemy-captured-relics)
			(or	(goal gl-wonder-build-attempt YES)
				(or	(players-building-type-count any-enemy wonder > 0)
					(or	(building-type-count-total wonder > 0)
						(or	(players-building-type-count any-ally wonder > 0)
							(goal gl-wonder-build-attempt-stage lost)))))))
=>
	(up-jump-rule 5)
)

(defrule
	(current-age-time > 300)
=>
	(up-jump-rule 2)
)

(defrule
	(players-building-type-count every-enemy wonder == 0)
	(goal gl-current-age gv-imperial)
	(or	(up-compare-goal gl-enemy-human-rating <= AVERAGE)
		(players-current-age every-enemy <= castle-age))
	(current-age-time < 300)
	(dropsite-min-distance stone < 4)
	(goal gl-strategy default)
	(game-time < 3300)
	(goal gl-allow-wonders YES)
=>
	(chat-to-player my-player-number "1Checking if wonder is a valid idea")
	(up-get-fact military-population 0 gl-my-soldier-count)
	(up-modify-goal gl-my-soldier-count c:- 15)
)

(defrule
	(players-building-type-count every-enemy wonder == 0)
	(goal gl-current-age gv-imperial)
	(or	(up-compare-goal gl-enemy-human-rating <= AVERAGE)
		(players-current-age every-enemy <= castle-age))
	(current-age-time < 300)
	(dropsite-min-distance stone < 4)
	(goal gl-strategy default)
	(game-time < 3300)
	(players-military-population any-human-enemy g:<= gl-my-soldier-count)
	(goal gl-allow-wonders YES)
=>
	(chat-to-player my-player-number "1Wonder!")
	(set-goal gl-wonder-build-attempt YES)
	(set-goal gl-wonder-build-attempt-stage saving-resources)
)

(defrule
	(game-time > wonder-game-time)
	(players-building-type-count every-enemy wonder == 0)
	(players-building-type-count every-ally wonder == 0)
	(population >= pop-to-full-pop-attack)
	(player-in-game every-ally)
	(unit-type-count trade-cart > 30)
	(goal POSITION POCKET)
	(strategic-number sn-allow-resource-spending > 4)
	(dropsite-min-distance stone < 4)
	(goal gl-strategy default)
	(goal gl-allow-wonders YES)
=>
	(chat-to-allies "1Attempting to build a wonder")
	(set-goal gl-wonder-build-attempt YES)
	(set-goal gl-wonder-build-attempt-stage saving-resources)
	(disable-self)
)

(defrule
	(game-time > wonder-game-time)
	(players-building-type-count every-enemy wonder == 0)
	(players-building-type-count every-ally wonder == 0)
	(population >= pop-to-full-pop-attack)
	(gold-amount > 1000)
	(strategic-number sn-allow-resource-spending > 4)
	(goal gl-strategy default)
	(goal gl-allow-wonders YES)
=>
	(chat-to-allies "1Attempting to build a wonder")
	(set-goal gl-wonder-build-attempt YES)
	(set-goal gl-wonder-build-attempt-stage saving-resources)
	(disable-self)
)

#load-if-defined DIFFICULTY-HARDEST
(defrule
	(current-age == imperial-age)
	(not	(enemy-captured-relics))
	(players-building-type-count every-enemy wonder == 0)
	(players-building-type-count every-ally wonder == 0)
	(population > 120)
	(game-time > wonder-game-time)
	(or	(goal POSITION POCKET)
		(not	(player-in-game any-ally)))
	(strategic-number sn-minimum-water-body-size-for-dock <= 1000)
=>
	(chat-to-allies "1Attempting to build a wonder")
	(set-goal gl-wonder-build-attempt YES)
	(set-goal gl-wonder-build-attempt-stage saving-resources)
	(disable-self)
)

(defrule
	(current-age == imperial-age)
	(game-time > wonder-game-time)
	(goal gl-wonder-build-attempt YES)
	(strategic-number sn-minimum-water-body-size-for-dock <= 1000)
=>
	(cc-add-resource food 2500)
	(cc-add-resource wood 2500)
	(cc-add-resource gold 2500)
	(cc-add-resource stone 1300)
	(disable-self)
)
#end-if

#load-if-not-defined UP-ALLY-IN-GAME
#load-if-defined AZTEC-CIV
(defrule
	(goal gl-strategy jaguar-boom)
	(up-compare-goal gl-enemy-human-rating <= AVERAGE)
	(research-completed my-unique-unit-upgrade)
	(research-completed ri-theocracy)
	(nor	(players-building-type-count any-enemy wonder > 0)
		(enemy-captured-relics))
	(dropsite-min-distance stone < 4)
=>
	(set-strategic-number sn-stone-gatherer-percentage 10)
	(set-goal gl-wonder-build-attempt YES)
	(set-goal gl-wonder-build-attempt-stage saving-resources)
	(disable-self)
)
#end-if
#end-if

#end-if
#end-if