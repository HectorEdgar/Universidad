(defrule
	(strategic-number G-AGE-STATUS >= GV-ADVANCING-TO-FEUDAL)
	(strategic-number G-AGE-STATUS < GV-ADVANCING-TO-CASTLE)
	(goal G-FEUDAL-STRATEGY  GV-FLUSH-SCOUT-ARCHERS)
	(taunt-detected my-player-number TA-INITIAL-AGE-FINISHED)
=>
	(set-goal G-JOLLYGOAL GV-FLUSH-SCOUT-ARCHERS)
)


; Warning about strategy to allies
(defrule
	(goal G-JOLLYGOAL GV-FLUSH-SCOUT-ARCHERS)
=>
	(chat-to-allies "I'm going to flush with scouts+archers")
	(chat-to-allies "Possible changes with unit selection to counter")
	(chat-to-player my-player-number "I'm going to flush with scouts+archers")
	(chat-to-player my-player-number "Possible changes with unit selection to counter")
	(disable-self)
)

; Scanning building phase

(defrule
	(goal G-JOLLYGOAL GV-FLUSH-SCOUT-ARCHERS)
	(or	(building-type-count-total barracks == 0)
		(or	(building-type-count-total archery-range == 0)
			(or	(building-type-count-total stable == 0)
				(dropsite-min-distance gold > IDEAL-DROP-DISTANCE)
			)
		)
	)
	(or	(strategic-number sn-food-gatherer-percentage != 40)
		(strategic-number sn-wood-gatherer-percentage != 60)
	)
=>
	(set-strategic-number sn-food-gatherer-percentage 40)
	(set-strategic-number sn-wood-gatherer-percentage 60)
	(set-strategic-number sn-gold-gatherer-percentage 0)
	(set-strategic-number sn-stone-gatherer-percentage 0)
)

(defrule
	(goal G-JOLLYGOAL GV-FLUSH-SCOUT-ARCHERS)
	(building-type-count-total barracks >= 1)
	(building-type-count-total archery-range >= 1)
	(building-type-count-total stable >= 1)
	(dropsite-min-distance gold <= IDEAL-DROP-DISTANCE)
	(or	(building-type-count-total blacksmith < 1)
		(building-type-count-total archery-range < 2)
	)
	(or	(strategic-number sn-food-gatherer-percentage != 42)
		(strategic-number sn-wood-gatherer-percentage != 48)
	)
=>
	(set-strategic-number sn-food-gatherer-percentage 42)
	(set-strategic-number sn-wood-gatherer-percentage 48)
	(set-strategic-number sn-gold-gatherer-percentage 10)
	(set-strategic-number sn-stone-gatherer-percentage 0)
)

(defrule
	(goal G-JOLLYGOAL GV-FLUSH-SCOUT-ARCHERS)
	(building-type-count-total barracks >= 1)
	(building-type-count-total archery-range >= 2)
	(building-type-count-total stable >= 1)
	(dropsite-min-distance gold <= IDEAL-DROP-DISTANCE)
	(building-type-count-total blacksmith >= 1)
	(or	(strategic-number sn-food-gatherer-percentage != 42)
		(strategic-number sn-wood-gatherer-percentage != 35)
	)
=>
	(set-strategic-number sn-food-gatherer-percentage 42)
	(set-strategic-number sn-wood-gatherer-percentage 35)
	(set-strategic-number sn-gold-gatherer-percentage 17)
	(set-strategic-number sn-stone-gatherer-percentage 0)
	(set-goal G-JOLLYGOAL GV-LATE-FEUDAL-0)
)